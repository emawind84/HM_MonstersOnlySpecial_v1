

actor D4Cacodemon : Cacodemon
{
 
    Health 350
	Radius 31
	Height 56
	Mass 8000
	Speed 7
	PainChance 64
	Monster
	+FLOAT +NOGRAVITY
	SeeSound 	"caco/sight"
	PainSound 	"caco/pain"
	DeathSound 	"caco/death"
	ActiveSound "cacodemon/idle"
	Obituary "$OB_CACO"
	HitObituary "$OB_CACOHIT"
	BloodColor "DarkBlue"
	Scale 0.80
  states
  {
  Spawn:
    CYBI ABCDEFGHIJKLMNOP 5 A_LookEx(0,0,0,0,120,"See")
    loop
  See:
    CYBW ABCDEFGHIJKLMNOPQRSTUVW 2 A_Chase
    loop
  Pain:
    CYBI A 3
    CYBI A 3 A_Pain
    goto See
  Missile:
    CYBF ABCD 3 A_FaceTarget
	CYBF E 0 A_PlaySound("Doom4/Monsters/Cacodemon/Attack",7)
	CYBF E 3 A_CustomMissile("D4CacodemonBall",32)
	CYBF FGHIJKLM 3 A_FaceTarget
	goto See
  Melee:
    CYBM ABC 3 A_FaceTarget
    TNT1 A 0 A_PlaySound("Doom4/Monsters/Cacodemon/Attack",7)
	CYBM D 3 A_CustomMeleeAttack(random(25,40),"Caco/Attack","weapons/fistswing",melee,1)
	CYBM EFGHIJKLM 3
	goto See
  Death:
	CYBI A 1 A_Scream
	CYBI A 1 A_Fall
	CYBI A 1 A_SpawnItemEx("D4CacodemonBody",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
	stop
  }
}

Actor D4CacodemonBody
{
	+BRIGHT
	Scale 0.80
	States
	{
	Spawn:
		CCDE A 1 A_CheckFloor("Land")
		Loop
	Land:
		CCDE BCDEFGH 2
		CCDE I 700
		CCDE IIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIII 1 A_FadeOut(0.06)
		Stop
	}
}

actor D4CacodemonBall
{
	Radius 6
	Height 8
	Speed 16
	FastSpeed 24
	Damage 3
	Projectile 
	+RANDOMIZE
	RenderStyle Add
	Alpha 0.99
	SeeSound "Doom4/Monsters/Cacodemon/Fireball"
	DeathSound "Doom4/Other/FireballHit1"
	+FORCEXYBILLBOARD
	Scale 0.4
	Decal "CacoDecal"
	+SEEKERMISSILE
	+FORCEXYBILLBOARD
	MaxTargetRange 2
	DamageType "Cacoball"
	States
	{
	Spawn:
		DB50 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Light("PurpleMediumPoint2") bright 
		loop
	Death:
		TNT1 A 0 A_PlaySound("Doom4/Other/FireballHit1",CHAN_AUTO,1.0,false,0.95) //in addition to the deathsound
		TNT1 A 0 A_SpawnItemEx("D4CacoBallExplo",0,0,0,0,0,0,0,32)
		TNT1 AAA 0 A_SpawnItemEx("CacoExploLightning",0,0,0,0,0,0,0,32)
		TNT1 A 2 bright Light("PurpleBigPoint3")
		TNT1 A 2 bright Light("PurpleMediumPoint1")
		TNT1 A 2 bright Light("PurpleMediumPoint2")
		TNT1 A 2 bright Light("PurpleMediumPoint3")
		TNT1 A 2 bright Light("PurpleSmallPoint1")
		TNT1 A 2 bright Light("PurpleSmallPoint2")
		TNT1 A 2 bright Light("PurpleSmallPoint3")
		stop
	}
}

Actor D4CacoBallExplo
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	RenderStyle Add
	Scale 0.2
	Translation "0:255=%[0.0,0.0,0.0]:[1.2,0.0,0.0]"
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		X060 B 1 Bright A_SetScale(scalex+0.08)	TNT1 A 0 A_FadeOut(0.06)
		X060 B 1 Bright A_SetScale(scalex+0.08)	TNT1 A 0 A_FadeOut(0.06)
		X060 B 1 Bright A_SetScale(scalex+0.08)	TNT1 A 0 A_FadeOut(0.06)
		X060 B 1 Bright A_SetScale(scalex+0.08)	TNT1 A 0 A_FadeOut(0.06)
		X060 B 1 Bright A_SetScale(scalex+0.08)	TNT1 A 0 A_FadeOut(0.06)
		X060 B 1 Bright A_SetScale(scalex+0.08)	TNT1 A 0 A_FadeOut(0.06)
		X060 B 1 Bright A_SetScale(scalex+0.08)	TNT1 A 0 A_FadeOut(0.06)
		X060 B 1 Bright A_SetScale(scalex+0.08)	TNT1 A 0 A_FadeOut(0.06)
	Looper:
		X060 B 1 Bright A_SetScale(scalex+0.08)
		X060 B 0  A_FadeOut(0.1)
		Loop
	}
}

Actor D4CacoExploFlash
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	RenderStyle Add
	Alpha 0.8
	Scale 1.7
	+FORCEXYBILLBOARD
	Translation "0:255=%[0.0,0.0,0.0]:[1.2,0.0,0.5]"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"T1","T2","T3","T4","T5","T6","T7","T8")
	T1:
		X153 E 1 A_FadeOut(0.018)
		Wait
	T2:
		X153 F 1 A_FadeOut(0.018)
		Wait
	T3:
		X153 G 1 A_FadeOut(0.018)
		Wait
	T4:
		X153 H 1 A_FadeOut(0.018)
		Wait
	T5:
		X153 I 1 A_FadeOut(0.018)
		Wait
	T6:
		X153 J 1 A_FadeOut(0.018)
		Wait
	T7:
		X153 K 1 A_FadeOut(0.018)
		Wait
	T8:
		X153 L 1 A_FadeOut(0.018)
		Wait
	}
}



Actor CacoExploLightning
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 0.99
	scale 0.6
	Translation "0:255=%[0.0,0.0,0.0]:[1.2,0.0,0.5]"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"T1","T2","T3","T4","T5")
	T1:	DLI1 A 0 
		Goto PlayOff
	T2:	DLI2 A 0 
		Goto PlayOff
	T3:	DLI3 A 0 
		Goto PlayOff
	T4:	DLI4 A 0 
		Goto PlayOff
	T5:	DLI5 A 0 
		Goto PlayOff
		
	PlayOff:
		"####" A 0 A_Jump(128,"FromL")
	FromA:
		"####" ABCDEFGHIJK 1 Bright
		Stop
	FromL:
		"####" LMNOPQRSTUV 1 Bright
		Stop
		
	}
}


Actor Cacospawner : RandomSpawner Replaces Cacodemon 
{

     DropItem "D4Cacodemon" ,255 ,1

}

Actor Painspawner : RandomSpawner Replaces Painelemental 
{

     DropItem "D4Cacodemon" ,255 ,1

}