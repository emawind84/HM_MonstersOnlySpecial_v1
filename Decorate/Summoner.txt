ACTOR D4Summoner : Archvile
{
	var int user_summonamount;
	var int user_teleporttime;
	
    Health 700
	Radius 20
	Height 56
	Mass 500
	Speed 4
	PainChance 10
	Monster
	+FLOORCLIP
	+DONTHARMSPECIES
	BloodColor Black
	SeeSound 	"Doom4/Monsters/Archvile/Sight"
	PainSound 	"Doom4/Monsters/Archvile/Pain"
	DeathSound 	"Doom4/Monsters/Archvile/Death"
	ActiveSound "Doom4/Monsters/Archvile/Idle"
	Obituary "$OB_VILE"
	Scale 0.70
	States
	{
	Spawn:
		CYBI A 0 A_SetSolid
		CYBI A 0 A_SetShootable
		CYBI A 0 A_Gravity
		CYBI A 0 A_ChangeFlag("FLOAT",FALSE)
		CYBI ABCDEFGHIJKLMNOPQRSTUVWXYZ 4 A_Look
		Loop
	See:
		CYBW A 0 A_SetSolid
		CYBW A 0 A_SetShootable
		CYBW A 0 A_Gravity
		CYBW A 0 A_ChangeFlag("FLOAT",FALSE)
		CYBW ABCDEFGHI 1 A_Chase
		CYBW J 0 A_Jump(64,"TeleportIn")
		CYBW JLMNOPQRS 1 A_Chase
		CYBW S 0 A_Jump(64,"TeleportIn")
		Loop
	TeleportIn:
		CYTI A 0 A_SetUserVar("user_teleporttime",random(35,70))
		CYTI A 0 A_PlaySound("Summoner/TeleportIn",5)
		CYTI ABCDEFGHIJKL 1 A_Recoil(-1)
		NADA A 0 A_UnsetShootable
		NADA A 0 A_UnsetSolid
		NADA A 0 A_NoGravity
		NADA A 0 A_ChangeFlag("FLOAT",TRUE)
		NADA A 1 A_SpawnItemEx("SummonerTeleportFlare",0,0,Height/2)
		Goto TeleportMove
	TeleportMove:
		NADA A 0 A_JumpIf(user_teleporttime<=0,"TeleportOut")
		NADA A 0 A_SetUserVar("user_teleporttime",user_teleporttime-1)
		NADA A 0 A_PlaySound("Summoner/TeleportLoop",6,1.0,1)
		NADA A 0 A_PlaySound("Summoner/TeleportLoop",7,1.0,1)
		NADA A 0 A_SpawnItemEx("ArgentLightningBolt",frandom(-24.0,24.0),0,frandom(24.0,72.0),frandom(1.0,2.0),0,0,0)
		NADA A 0 A_SpawnItemEx("ArgentSmoke",frandom(-32.0,32.0),0,frandom(16.0,80.0),frandom(1.0,2.0),0,frandom(-2.0,2.0),frandom(0.0,359.9))
		NADA A 1 A_Chase(0,0,CHF_NOPLAYACTIVE)
		Loop
	TeleportOut:
		NADA A 0 A_SpawnItemEx("SummonerTeleportFlare",0,0,Height/2)
		NADA A 0 A_SetShootable
		NADA A 0 A_SetSolid
		NADA A 0 A_Recoil(-24)
		NADA A 0 A_Gravity
		NADA A 0 A_ChangeFlag("FLOAT",FALSE)
		CYTO A 1 A_PlaySound("Summoner/TeleportOut",6)
		CYTO A 1 A_PlaySound("Summoner/TeleportOut",7)
		CYTO BCDEFGHIJKLM 1
		Goto See
	Missile:
		CYBF A 0 A_Jump(64,"SummonStart")
		CYBF A 0 A_PlaySound("Summoner/Attack")
	    CYBF ABCDEFG 2 A_FaceTarget
		CYBF H 1 A_CustomMissile("SummonerScythe",48)
		CYBF H 1 A_PlaySound("Summoner/FireBall",5)
		CYBF IJKLMNOPQRSTU 2
		CYBF U 0 A_Jump(96,"TeleportIn","DodgeLeft","DodgeRight")
		Goto See
	SummonStart:
		CYSS A 0
		CYSS A 0 A_ChangeFlag("NOPAIN",TRUE)
		CYSS A 0 A_NoGravity
		CYSS A 0 A_Stop
		CYSS A 0 A_PlaySound("Summoner/PortalChannel",5)
		CYSS A 0 A_SpawnItemEx("SummonerBreachPortal",0,0,160,0,0,0,0,SXF_SETMASTER)
		CYSS ABCDEFGHIJKLMNOPQRSTU 1
		Goto SummonLoop
	SummonLoop:
		CYSL A 0
		CYSL A 0 
		CYSL ABCDEFGHIJKLMN 2
		CYSL O 0 A_SpawnItemEx("SummonerMonsterSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		CYSL O 0 A_SpawnItemEx("SummonerMonsterSpawner",0,0,160,0,0,0,0,SXF_NOCHECKPOSITION)
		CYSL O 0 A_AlertMonsters(1600)
		CYSL O 0 A_Stop
		CYSL O 0
		Goto SummonEnd
	SummonEnd:
		CYSE A 0 A_PlaySound("Summoner/PortalStop",6)
		CYSE A 0 A_ChangeFlag("NOPAIN",FALSE)
		CYSS A 0 A_Gravity
		CYSE A 0 A_KillChildren
		CYSE ABCDEFGHIJKLMNOPQRST 1 A_AlertMonsters(1600)
		CYSE U 2 A_Jump(96,"TeleportIn")
		Goto See
	Pain:
		CYBI A 1
		CYBI A 1 A_Pain
		CYBI A 0 A_Gravity
		CYBI A 0 A_SetShootable
		CYBI A 0 A_SetSolid
		CYBI A 0 A_ChangeFlag("FLOAT",FALSE)
		CYBI A 0 A_Jump(250,"DodgeLeft","DodgeRight")
		Goto See
	DodgeLeft:
		CYDL A 1 A_ChangeVelocity(0,24,0,CVF_RELATIVE)
		CYDL BCDEFGHIJKLMNOPQR 1
		Goto See
	DodgeRight:
		CYDR A 1 A_ChangeVelocity(0,-24,0,CVF_RELATIVE)
		CYDR BCDEFGHIJKLMNOPQR 1
		Goto See
	Death:
		CYBI A 1 A_Scream
		CYBI A 1 A_Fall
		TNT1 A 1 A_SpawnItemEx("D4SummonerBody",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}

Actor D4SummonerBody
{
	Gravity 0.25
	Scale 0.70
	States
	{
	Spawn:
		CYBD A 3 A_Recoil(4)
		CYBD BCDEFGHIJKLMNOPQRSTU 3
		CYBD V 700
		Goto FadeOut
	FadeOut:
		CYBD V 1 A_FadeOut(0.06)
		Loop
	}
}

actor SummonerFloorPortal
{
  Height 4
  Radius 2
  Mass 0x7FFFFFFF
  Health 0x7FFFFFFF
  +THRUACTORS
  +SHOOTABLE
  +ISMONSTER
  +NOGRAVITY
  +BRIGHT
  -SOLID
  States
  {
  Spawn:
	NADA B 4 NoDelay
	NADA A 1
	Goto Idle
  Idle:
	NADA A 1
	Loop
  Death:
	NADA A 8 A_Stop
	NADA B 1
	Stop
  }
}

actor SummonerFloorPortalBorder : SummonerFloorPortal
{
	Alpha 1.0
	RenderStyle Add
}

ACTOR SummonerMeleePulse
{
   Height 1
   Radius 1
   Scale 5.0
   RenderStyle Add
   Alpha 1.0
   DamageType "Monsters"
   +BRIGHT
   +FORCEXYBILLBOARD
   +NOGRAVITY
   States
   {
   Spawn:
	  BERZ F 0 NoDelay A_SpawnItemEx("ArgentLightningRing",0,0,0)
	  BERZ FFFFFFFFFFFFFFF 0 A_SpawnItemEx("ArgentSmoke",frandom(-32.0,32.0),0,frandom(-32.0,32.0),frandom(1.0,4.0),0,frandom(-4.0,4.0),frandom(0.0,359.9))
	  BERZ F 1 A_Explode(100,160,0,0,160)
	  Goto ScaleDown
   ScaleDown:
	  BERZ F 0 A_JumpIf(ScaleX<=0.0,"Death")
	  BERZ F 1 A_SetScale(ScaleX-0.25)
      Loop
   Death:
	  BERZ A 1
      Stop
   }
}

actor SummonerScythe : FastProjectile
{
    Radius 8
	Height 16
	Speed 64
	Damage 5
	RenderStyle Add
	Alpha 1.0
	SeeSound "Doom4/Monsters/ArgentWaveFire"
	DeathSound "Summoner/BallImpact"
	Decal DoomImpScorch
	+MTHRUSPECIES
	States
	{
    Spawn:
		NADA AA 0 A_SpawnItemEx("ArgentSmokeSmall",0,0,frandom(-40.0,40.0),frandom(1.0,2.0),0,frandom(-2.0,2.0),frandom(0.0,359.9))
		NADA A 1 A_SpawnItemEx("ArgentSmokeSmall",0,0,frandom(-40.0,40.0),frandom(1.0,2.0),0,frandom(-2.0,2.0),frandom(0.0,359.9))
		Loop
	Death:
		BERZ F 0 A_SetScale(4.0)
		BERZ F 0 A_SpawnItemEx("ArgentLightningRing",0,0,0)
		BERZ FFFFFFFFFFFFFFF 0 A_SpawnItemEx("ArgentSmoke",frandom(-32.0,32.0),0,frandom(-32.0,32.0),frandom(1.0,4.0),0,frandom(-4.0,4.0),frandom(0.0,359.9))
		BERZ F 1 A_Explode(10,192,0,0,128)
		Goto ScaleDown
   ScaleDown:
		BERZ F 0 A_JumpIf(ScaleX<=0.0,"Vanish")
		BERZ F 1 A_SetScale(ScaleX-0.25)
		Loop
   Vanish:
		BERZ A 1
		Stop
	}
}

Actor SummonerMonsterSpawner
{
	Speed 12
	Radius 32
	Height 4
	Damage 0
	Projectile
	+THRUACTORS
	+DONTSPLASH
	-NOGRAVITY
	+FORCEXYBILLBOARD
	+BOUNCEONWALLS
	+SKYEXPLODE
	+STEPMISSILE
	MaxStepHeight 32
	BounceFactor 1.0
	WallBounceFactor 1.0
	RenderStyle None
	States
	{
	Spawn:
		BAL1 A 8
		BAL1 A 0 A_Stop
		Goto Death
	Death:
		TNT1 A 0 A_SpawnItemEx("SummonTeleportFog",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("TporDropBallSpawner",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	}
}

Actor TporDropBallSpawner : RandomSpawner
{
	DropItem "Zombieman"	, 255, 1
	DropItem "ShotgunGuy"	, 255, 1
	DropItem "DoomImp"	, 255, 1
	DropItem "Demon"		, 255, 1
	DropItem "LostSoul"	, 255, 1
	DropItem "Revenant"	, 255, 1
	DropItem "HellKnight"	, 255, 1
}

Actor ArgentSmoke
{
   Height 4
   Radius 2
   RenderStyle Add
   Alpha 0.25
   Scale 0.5
   +NOINTERACTION
   +ForceXYBillboard
   +CLIENTSIDEONLY
   +BRIGHT
   States
   {
   Spawn:
      DTMF A 0 A_FadeOut(0.005)
	  DTMF A 1 A_SetScale(ScaleX+0.01)
      Loop
   }
}

Actor ArgentSmokeSmall : ArgentSmoke
{
   Scale 0.35
   States
   {
   Spawn:
      DTMF A 0 A_FadeOut(0.01)
	  DTMF A 1 A_SetScale(ScaleX+0.01)
      Loop
   }
}

Actor ArgentLightningRing
{
	Radius 2
	Height 4
	RenderStyle Add
	Scale 0.35
	Alpha 1.0
	ReactionTime 12
	+NOINTERACTION
	+CLIENTSIDEONLY
	+ForceXYBillboard
	+BRIGHT
	States
	{
	Spawn:
		NADA A 0 NoDelay A_SetScale(ScaleX+0.025)
		NADA A 1 A_Jump(256,"SpriteA","SpriteB","SpriteC","SpriteD","SpriteE","SpriteF","SpriteG","SpriteH","SpriteI","SpriteJ","SpriteK","SpriteL","SpriteM","SpriteN","SpriteO")
		Loop
	SpriteA:
		SMEA A 1
		Goto Countdown
	SpriteB:
		SMEA B 1
		Goto Countdown
	SpriteC:
		SMEA C 1
		Goto Countdown
	SpriteD:
		SMEA D 1
		Goto Countdown
	SpriteE:
		SMEA E 1
		Goto Countdown
	SpriteF:
		SMEA F 1
		Goto Countdown
	SpriteG:
		SMEA G 1
		Goto Countdown
	SpriteH:
		SMEA H 1
		Goto Countdown
	SpriteI:
		SMEA I 1
		Goto Countdown
	SpriteJ:
		SMEA J 1
		Goto Countdown
	SpriteK:
		SMEA K 1
		Goto Countdown
	SpriteL:
		SMEA L 1
		Goto Countdown
	SpriteM:
		SMEA M 1
		Goto Countdown
	SpriteN:
		SMEA N 1
		Goto Countdown
	SpriteO:
		SMEA O 1
		Goto Countdown
	Countdown:
		NADA A 0 A_Countdown
		Goto Spawn
	Death:
		NADA A 1
		Stop
	}
}

Actor ArgentLightningBolt
{
	Radius 2
	Height 4
	RenderStyle Add
	Alpha 1.0
	Scale 0.25
	ReactionTime 4
	+NOINTERACTION
	+CLIENTSIDEONLY
	+ForceXYBillboard
	+BRIGHT
	States
	{
	Spawn:
		NADA A 0 NoDelay A_SetScale(ScaleX+0.1)
		NADA Z 1 A_Jump(256,"SpriteA","SpriteB","SpriteC","SpriteD")
		Loop
	SpriteA:
		NADA A 1
		Goto Countdown
	SpriteB:
		NADA B 1
		Goto Countdown
	SpriteC:
		NADA C 1
		Goto Countdown
	SpriteD:
		NADA D 1
		Goto Countdown
	Countdown:
		NADA A 0 A_Countdown
		Goto Spawn
	Death:
		NADA Z 1
		Stop
	}
}

ACTOR SummonerTeleportFlare
{
   Height 1
   Radius 1
   Scale 3.0
   RenderStyle Add
   Alpha 1.0
   +NOINTERACTION
   +CLIENTSIDEONLY
   +BRIGHT
   +FORCEXYBILLBOARD
   States
   {
   Spawn:
	  BERZ F 1 A_FadeOut(0.05)
      Loop
   }
}

Actor SummonerBreachPortal
{
  Radius 2
  Height 4
  Mass 0x7FFFFFFF
  Health 0x7FFFFFFF
  +THRUACTORS
  +SHOOTABLE
  +ISMONSTER
  +NOTARGET
  +NOGRAVITY
  +BRIGHT
  -SOLID
  States
  {
  Spawn:
	NADA A 0 A_SpawnItemEx("ArgentSpiral",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,96)
	NADA A 1 A_SpawnItemEx("ArgentLightningRing",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,232)
	Loop
  Death:
    NADA A 1 A_Stop
	Stop
  }
}

ACTOR ArgentSpiral
{
   Height 4
   Radius 2
   Scale 1.5
   RenderStyle Add
   Alpha 0.0
   +BRIGHT
   +NOINTERACTION
   +CLIENTSIDEONLY
   +FORCEXYBILLBOARD
   States
   {
   Spawn:
	  DTMB A 0 A_JumpIf(Alpha>=0.25,"Delet")
	  DTMB A 0 A_FadeIn(0.01)
	  DTMB A 1 A_SetScale(ScaleX-0.025)
	  DTMB B 0 A_JumpIf(Alpha>=0.25,"Delet")
	  DTMB B 0 A_FadeIn(0.01)
	  DTMB B 1 A_SetScale(ScaleX-0.025)
	  DTMB C 0 A_JumpIf(Alpha>=0.25,"Delet")
	  DTMB C 0 A_FadeIn(0.01)
	  DTMB C 1 A_SetScale(ScaleX-0.025)
	  DTMB D 0 A_JumpIf(Alpha>=0.25,"Delet")
	  DTMB D 0 A_FadeIn(0.01)
	  DTMB D 1 A_SetScale(ScaleX-0.025)
	  DTMB E 0 A_JumpIf(Alpha>=0.25,"Delet")
	  DTMB E 0 A_FadeIn(0.01)
	  DTMB E 1 A_SetScale(ScaleX-0.025)
	  DTMB F 0 A_JumpIf(Alpha>=0.25,"Delet")
	  DTMB F 0 A_FadeIn(0.01)
	  DTMB F 1 A_SetScale(ScaleX-0.025)
	  DTMB G 0 A_JumpIf(Alpha>=0.25,"Delet")
	  DTMB G 0 A_FadeIn(0.01)
	  DTMB G 1 A_SetScale(ScaleX-0.025)
	  DTMB H 0 A_JumpIf(Alpha>=0.25,"Delet")
	  DTMB H 0 A_FadeIn(0.01)
	  DTMB H 1 A_SetScale(ScaleX-0.025)
	  Loop
   Delet:
	NADA A 1
	Stop
	}
}

actor ArgentSlowDebuff : PowerupGiver
{
 	Powerup.Color "FF 00 00", 0.0
 	Powerup.Type ArgentSlower
 	Powerup.Duration 70
 	+INVENTORY.AUTOACTIVATE
 	+INVENTORY.ALWAYSPICKUP
    states
 	{
 	Spawn:
   		NADA A 1
   		loop
 }
}

ACTOR PowerArgentSlower : PowerSpeed
{
  Powerup.Duration 70
  Speed 0.5
  +POWERSPEED.NOTRAIL
  +INVENTORY.ADDITIVETIME
}


Actor Archspawner : RandomSpawner Replaces Archvile
{

    DropItem "D4Summoner" ,255 ,1

}