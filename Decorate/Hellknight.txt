

actor D4Hellknight : Hellknight
{
    Health 350
	Radius 24
	Height 64
	Mass 400
	Speed 4
	PainChance 84
	Monster
	//MaxDropOffHeight 120
	+FLOORCLIP
	+MISSILEMORE
	+MISSILEEVENMORE
	+DONTHARMSPECIES
	SeeSound 	"Doom4/Monsters/HellKnight/Sight"
	ActiveSound "Doom4/Monsters/HellKnight/Idle"
	PainSound 	"Doom4/Monsters/Baron/Pain"
	DeathSound 	"Doom4/Monsters/HellKnight/Death"
	HitObituary "$OB_KNIGHTHIT"
	Obituary "$OB_KNIGHT"
	Scale 0.70
	Gravity 0.83
	Damage 3
    BloodColor "DarkGreen"
	Species "Hell Knight"
	MaxTargetRange 512
	MeleeRange 70
	MeleeThreshold 196
  states
  {
  Spawn:
    CYBI ABCDEFGHIJK 4 A_LookEx(0,0,0,0,90,"See")
    loop
  See:
    CYBW A 1 A_Chase
	CYBW B 0
	CYBW B 0 
	CYBW B 0 
	CYBW BCDEFGHIJK 1 A_Chase
	CYBW L 0
	CYBW L 0
	CYBW LMNOPQRSTU 1 A_Chase
    loop
  Missile:
    CYBW A 0 A_Jump(30,"LeapRight")
    goto See
  LeapRight:
    CY1L ABCD 1 A_FaceTarget
    CY1L E 1 A_ChangeVelocity(24,0,8,CVF_RELATIVE)
	CY1L FGHI 1 A_CheckFloor("LeapRightLand")
    goto LeapRightLoop
  LeapRightLoop:
    CY1L J 2 A_CheckFloor("LeapRightLand")
	Loop
  LeapRightLand:
    CY1L K 0 A_Quake(2,4,0,384,"")
	CY1L K 0 A_Quake(1,8,0,512,"")
	CY1L K 0 A_PlaySound("cyberland",5)
	CY1L K 2 A_Stop
	CY1L L 2 A_SpawnItemEx("HellknightLeapExplosion",8,0,8,0,0,0,0,SXF_NOCHECKPOSITION)
    CY1L MNOP 2
	CY1L QRSTU 3
	goto See
  Pain:
    CYBI A 1
    CYBI A 1 A_Pain
    goto See
  Melee:
    CYBM A 0 A_Jump(48,"MeleeAlt")
    CYBM ABCDEFGHIJK 1 A_FaceTarget
    CYBM L 0 A_PlaySound("Monsters/HellKnight/Melee",5)
	CYBM L 2 A_CustomMeleeAttack(random(8,10),"Melee/Punch","Doom4/Monsters/HellKnight/Melee",melee,1)
	CYBM MNOPQRSTU 2
	goto See
  MeleeAlt:
    CYBN ABCDEFG 2 A_FaceTarget
	CYBN H 2 A_SpawnItemEx("HellknightLeapExplosion",8,0,8,0,0,0,0,SXF_NOCHECKPOSITION)
	CYBN IJKLMN 2
    goto See
  Death:
	CYBI A 1 A_Scream
	CYBI A 1 A_Fall
	CYBI A 1 A_SpawnItemEx("D4HellknightBody",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
	stop
  }
}

Actor D4HellknightBody
{
	Scale 0.70
	States
	{
	Spawn:
		HKDE ABCDEFGHIJKLMNOPQR 4
		HKDE S 700
		Goto FadeOut
    FadeOut:
		HKDE S 1 A_FadeOut(0.06)
		Loop
	}
}

ACTOR HellknightLeapExplosion
{
   Radius 8
   Height 16
   RenderStyle Add
   Alpha 1.0
   Scale 1.5
   DamageType "Melee"
   Obituary "$OB_HKMELEE"
   +ForceXYBillboard
   +MISSILE
   +BRIGHT
   +EXTREMEDEATH
   States
   {
   Spawn:
	   STAS F 0
       STAS FFFFFFFFFF 0 A_SpawnItemEx("HellknightLeapFire", 0,0,0, Random(2, 4), 0, Random(2, 4), Random(0, 360))
	   STAS F 0 A_PlaySound("Doom4/Monsters/BaronFloorPound",5)
	   STAS F 0 A_PlaySound("Doom4/Monsters/BaronFloorPound",6)
	   STAS F 6 A_Explode(25,128,0,0,128)
       Stop
   }
}

Actor HellknightLeapFire
{
   Height 2
   Radius 1
   RenderStyle Add
   Alpha 0.5
   Scale 0.1
   +NOINTERACTION
   +ForceXYBillboard
   +CLIENTSIDEONLY
   +BRIGHT
   States
   {
   Spawn:
      GASC A 0 A_FadeOut(0.05)
	  GASC A 1 A_SetScale(ScaleX+0.1)
      Loop
   }
}

Actor Hellknightspawner : RandomSpawner Replaces Hellknight
{

     DropItem "D4Hellknight" ,255 ,1

}